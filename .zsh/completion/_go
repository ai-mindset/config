
#compdef go

_go() {
    local -a commands
    commands=(
        'bug:start a bug report'
        'build:compile packages and dependencies'
        'clean:remove object files and cached files'
        'doc:show documentation for package or symbol'
        'env:print Go environment information'
        'fix:update packages to use new APIs'
        'fmt:gofmt (reformat) package sources'
        'generate:generate Go files by processing source'
        'get:add dependencies to current module and install them'
        'install:compile and install packages and dependencies'
        'list:list packages or modules'
        'mod:module maintenance'
        'work:workspace maintenance'
        'run:compile and run Go program'
        'telemetry:manage telemetry data and settings'
        'test:test packages'
        'tool:run specified go tool'
        'version:print Go version'
        'vet:report likely mistakes in packages'
    )

    local -a help_topics
    help_topics=(
        'buildconstraint:build constraints'
        'buildjson:build -json encoding'
        'buildmode:build modes'
        'c:calling between Go and C'
        'cache:build and test caching'
        'environment:environment variables'
        'filetype:file types'
        'goauth:GOAUTH environment variable'
        'go.mod:the go.mod file'
        'gopath:GOPATH environment variable'
        'goproxy:module proxy protocol'
        'importpath:import path syntax'
        'modules:modules, module versions, and more'
        'module-auth:module authentication using go.sum'
        'packages:package lists and patterns'
        'private:configuration for downloading non-public code'
        'testflag:testing flags'
        'testfunc:testing functions'
        'vcs:controlling version control with GOVCS'
    )

    _arguments -C \
        '1: :->command' \
        '*:: :->args'

    case $state in
        command)
            _describe -t commands 'go command' commands
            _describe -t help_topics 'help topic (go help <topic>)' help_topics
            ;;
        args)
            case $words[1] in
                build|install|run|test|vet|fix|fmt|generate|get|list|clean)
                    _files -g "*.go"
                    ;;
                doc)
                    _files -g "*.go"
                    ;;
                mod)
                    local -a mod_cmds
                    mod_cmds=('download' 'edit' 'graph' 'init' 'tidy' 'vendor' 'verify' 'why')
                    _describe -t mod_cmds 'mod command' mod_cmds
                    ;;
                work)
                    local -a work_cmds
                    work_cmds=('edit' 'init' 'sync' 'use' 'vendor')
                    _describe -t work_cmds 'work command' work_cmds
                    ;;
                telemetry)
                    local -a telemetry_cmds
                    telemetry_cmds=('on' 'off' 'local')
                    _describe -t telemetry_cmds 'telemetry command' telemetry_cmds
                    ;;
                tool)
                    local -a tools
                    tools=(${(f)"$(go tool 2>/dev/null)"})
                    _describe -t tools 'go tool' tools
                    ;;
                env)
                    local -a env_vars
                    env_vars=(${(f)"$(go env 2>/dev/null | cut -d= -f1)"})
                    _describe -t env_vars 'environment variable' env_vars
                    ;;
            esac
            ;;
    esac
}

_go "$@"

